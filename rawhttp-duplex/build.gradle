plugins {
    id 'java-library'
    id "org.jetbrains.kotlin.jvm"
    id 'maven-publish'
    id 'signing'
}

version rawHttpDuplexVersion
description 'rawhttp-duplex provides a mechanism for full duplex communications based on the ' +
        'HTTP/1.1 chunked transfer-encoding.'

compileTestKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

configurations.create('exampleApp')

dependencies {
    api project(':rawhttp-core')
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    testImplementation "org.jetbrains.kotlin:kotlin-reflect"
    testImplementation "io.kotest:kotest-runner-junit5:$kotestVersion"
    testImplementation "io.kotest:kotest-assertions-core:$kotestVersion"

    exampleApp project(':rawhttp-core')
    exampleApp "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

task chatAppFatJar(type: Jar) {
    dependsOn 'compileTestKotlin'
    manifest {
        attributes 'Main-Class': 'Chat_exampleKt'
    }

    archiveName = 'chat-app.jar'

    from jar
    from compileTestKotlin
    from { configurations.exampleApp.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}


jar {
    manifest {
        attributes 'Implementation-Title': 'rawhttp.duplex',
                'Implementation-Version': version,
                'Implementation-Vendor': 'Renato Athaydes',
                'Automatic-Module-Name': 'rawhttp.duplex'
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    classifier "sources"
    extension "jar"
}

task javadocJar(type: Jar) {
    classifier "javadoc"
    from javadoc
}

/* Publishing config */

publishing {
    publications {
        mavenJava( MavenPublication ) {
            artifactId = 'rawhttp-duplex'
            from components.java
            versionMapping {
                usage( 'java-api' ) {
                    fromResolutionOf( 'runtimeClasspath' )
                }
                usage( 'java-runtime' ) {
                    fromResolutionResult()
                }
            }
            pom {
                inceptionYear = '2018'
                name = project.name
                packaging = 'jar'
                description = project.description

                url = 'https://renatoathaydes.github.io/rawhttp'

                scm {
                    connection = 'git@github.com:renatoathaydes/rawhttp.git'
                    developerConnection = 'git@github.com:renatoathaydes/rawhttp.git'
                    url = 'https://github.com/renatoathaydes/rawhttp'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'renatoathaydes'
                        name = 'Renato Athaydes'
                        email = 'renato@athaydes.com'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username project.properties[ 'ossrhUsername' ]?.toString()
                password project.properties[ 'ossrhPassword' ]?.toString()
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
